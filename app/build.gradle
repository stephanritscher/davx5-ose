/***************************************************************************************************
 * Copyright Â© All Contributors. See LICENSE and AUTHORS in the root directory for details.
 **************************************************************************************************/

apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'

    defaultConfig {
        applicationId "at.bitfire.davdroid"

        versionCode 402010002
        versionName '4.2.1-rc.1-ose'
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 23        // Android 5
        targetSdkVersion 32     // Android 12

        buildConfigField "String", "userAgent", "\"DAVx5\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //testInstrumentationRunnerArgument "notAnnotation", "androidx.test.filters.FlakyTest"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    compileOptions {
        // enable because ical4android requires desugaring
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    flavorDimensions "distribution"
    productFlavors {
        standard {
            versionNameSuffix "-ose"
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-release.pro'

            shrinkResources true
        }
    }

    lint {
        disable 'GoogleAppIndexingWarning', 'ImpliedQuantity', 'MissingQuantity', 'MissingTranslation', 'ExtraTranslation', 'RtlEnabled', 'RtlHardcoded', 'Typos', 'NullSafeMutableLiveData'
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/*.md']
        }
    }
}

dependencies {
    implementation project(':InteractiveKeyManager')
    implementation project(':cert4android')
    implementation project(':ical4android')
    implementation project(':vcard4android')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.5.0'

    def room_version = '2.4.2'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation "com.github.AppIntro:AppIntro:${versions.appIntro}"
    implementation("com.github.bitfireAT:dav4jvm:${versions.dav4jvm}") {
        exclude group: 'junit'
    }
    implementation "com.mikepenz:aboutlibraries:${versions.aboutLibraries}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:okhttp-brotli:${versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
    implementation 'commons-io:commons-io:2.6'
    //noinspection GradleDependency - dnsjava 3+ needs Java 8/Android 7
    implementation 'dnsjava:dnsjava:2.1.9'
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-collections4:${versions.commonsCollections}"
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-lang3:${versions.commonsLang}"
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // for tests
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
}
